@APPTITLE "Balloons"
@APPAUTHOR "Enrique Mecklenburg Serkovic"
@APPVERSION "$VER: Balloons 1.5 (26-Dec-20)"
@APPDESCRIPTION "Scratch example written in Hollywood"
@APPICON {Ic16x16 = "icons/icon16x16.png",
		  Ic16x16s = "icons/icon16x16.png",
		  Ic32x32 = "icons/icon32x32.png",
		  Ic32x32s = "icons/icon32x32.png",
		  Ic64x64 = "icons/icon64x64.png",
		  Ic64x64s = "icons/icon64x64.png",
		  Ic128x128 = "icons/icon128x128.png",
		  Ic128x128s = "icons/icon128x128.png"}


/*********************************
*  Objective of the game is to   *
*  click on the balloons to pop  *
*  them before time ends.        *
*********************************/

@DISPLAY 1, {Width = 640, Height = 480, Color = #GRAY, Title = "Balloons"}

@BGPIC 2, "backgrounds/background.png"

;balloon sprites, each with 2 frames, one with the
;balloon image and one with the popped image
@BRUSH 1, "sprites/balloon_blue.png", {LoadAlpha = True}
@BRUSH 2, "sprites/balloon_black.png", {LoadAlpha = True}
@BRUSH 3, "sprites/balloon_green.png", {LoadAlpha = True}
@BRUSH 4, "sprites/balloon_red.png", {LoadAlpha = True}

;menu button brushes
@BRUSH 5, "buttons/red.png", {LoadAlpha = True}
@BRUSH 6, "buttons/blue.png", {LoadAlpha = True}
@BRUSH 7, "buttons/green.png", {LoadAlpha = True}
@BRUSH 8, "buttons/gray.png", {LoadAlpha = True}

;game samples
@SAMPLE 1, "sounds/menu.8svx"
@SAMPLE 2, "sounds/button.8svx"
@SAMPLE 3, "sounds/pop.8svx"
@SAMPLE 4, "sounds/quit.8svx"
@SAMPLE 5, "sounds/win.8svx"
@SAMPLE 6, "sounds/lose.8svx"

;include other source files
@INCLUDE "functions.hws"		;general functions
@INCLUDE "balloon.hws"			;balloon object functions

EscapeQuit(True)

SetFillStyle(#FILLCOLOR)


player = {name = "", score = 0, time = 0, date = ""}	;player's data
balloons = 15								;total ballons on screen
time = 10									;total time in seconds
countdown = {}								;table for storing time numbers
scores = {}									;table for storing score numbers
highscores = {}								;highscores table
done = True									;is game over?


/*******************************************
 * Mouse and keyboard events while playing *
 *******************************************/
Function p_EventFunction(msg)
    Switch(msg.action)
    Case "OnMouseDown":
	    If done = False
	        p_Burst()											;check if balloon is hit
		EndIf
    Case "OnKeyDown":
	    If msg.key = " " And HaveObject(#TIMER, 1)				;check if SPACE pressed and timer 1 is running
	        PauseTimer(1)										;pause timer
	        WaitKeyDown("Space")								;pause the script until SPACE is pressed again
	        ResumeTimer(1)
	    ElseIf (msg.key = "Y" Or msg.key = "y") And done		;restart if game over
	        p_CleanUp()
	        p_Play()
	    ElseIf (msg.key = "N" Or msg.key = "n") And done		;quit if game over
	        p_CleanUp()
			p_Start()
	    EndIf
    EndSwitch
EndFunction


/*****************************************
*  Event table function for menu buttons *
******************************************/
function p_Buttons(msg)
	Switch msg.action
	Case "OnMouseUp"
		If msg.id = button1
			PlaySample(2)
			p_Play()
		ElseIf msg.id = button2
			PlaySample(2)
			p_SetName()
		ElseIf msg.id = button3
			PlaySample(2)
			p_ShowScores()
		ElseIf msg.id = button4
			PlaySample(4)
			Wait(25)
			End()
		ElseIf msg.id = button5
			SelectDisplay(1)
			FreeDisplay(3)
		EndIf
	EndSwitch
EndFunction


/*****************
 * Main function *
 *****************/
Function p_Play()
    done = False			;game is not over

	If (player.name = "")	;if no player name yet...
		p_SetName()
	EndIf
	
	player.time = 0
	player.date = ""
	
	InstallEventHandler({OnMouseDown = p_EventFunction, OnKeyDown = p_EventFunction})

    SetFont(#SANS, 18, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)
	SetFontColor(#WHITE)

    DisplayTransitionFX(2, {Type = #FADE, Speed = 50, Parameter = #WHITE})	;display background with a transition fx

    Box(0, 0, 640, 25, #GRAY, {Transparency = "50%"})          ;score and time area

    p_CreateScore()                                            ;set starting score
    p_CreateCountdown()                                        ;set remaining time
    Wait(25)
    p_CreateBalloons()                                         ;create and show balloons

    StartTimer(1)                                              ;start timer

    SetInterval(1, p_Countdown, 1000)                          ;execute timecountdown loop, 1 second interval
    SetInterval(2, p_MoveBalloons, 1000/25)                    ;execute main loop at 25fps
EndFunction


/*************
* Start menu *
**************/
Function p_Start()
    SetFont(#SANS, 30, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)
	SetFillStyle(#FILLCOLOR)

	;events for start menu buttons
	evttable = {OnMouseUp = p_Buttons}

	DisplayTransitionFX(1, {Type = #FADE, Speed = 50, Parameter = #WHITE})	;display background with a transition fx

	PlaySample(1)					;play menu sample

	;show button brushes
	DisplayBrush(5, 220, 150, {Width = 150, Height = 50})
	DisplayBrush(6, 280, 215, {Width = 150, Height = 50})
	DisplayBrush(7, 220, 280, {Width = 150, Height = 50})
	DisplayBrush(8, 530, 420, {Width = 100, Height = 50})

	button1Text = CreateTextObject(Nil, "Play", {Encoding=utf8})
	DisplayTextObject(button1Text, 260, 160)
	button1Text = CreateTextObject(Nil, "Name", {Encoding=utf8})
	DisplayTextObject(button1Text, 330, 225)
	button1Text = CreateTextObject(Nil, "Scores", {Encoding=utf8})
	DisplayTextObject(button1Text, 240, 290)
	button1Text = CreateTextObject(Nil, "Exit", {Encoding=utf8})
	DisplayTextObject(button1Text, 555, 430)

	;create buttons
	button1 = MakeButton(Nil, #SIMPLEBUTTON, 220, 150, 150, 50, evttable)
	button2 = MakeButton(Nil, #SIMPLEBUTTON, 280, 215, 150, 50, evttable)
	button3 = MakeButton(Nil, #SIMPLEBUTTON, 220, 280, 150, 50, evttable)
	button4 = MakeButton(Nil, #SIMPLEBUTTON, 530, 420, 100, 50, evttable)
	
	p_OpenScores()
EndFunction


p_Start()    ;start game

Repeat
    WaitEvent()
Forever
