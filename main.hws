@APPTITLE "Balloons"
@APPAUTHOR "Enrique Mecklenburg Serkovic"
@APPVERSION "$VER: Balloons 1.6 (28-Dec-20)"
@APPDESCRIPTION "Scratch example written in Hollywood"
@APPICON {Ic16x16 = "icons/icon16x16.png",
		  Ic16x16s = "icons/icon16x16.png",
		  Ic32x32 = "icons/icon32x32.png",
		  Ic32x32s = "icons/icon32x32.png",
		  Ic64x64 = "icons/icon64x64.png",
		  Ic64x64s = "icons/icon64x64.png",
		  Ic128x128 = "icons/icon128x128.png",
		  Ic128x128s = "icons/icon128x128.png"}


/*********************************
*  Objective of the game is to   *
*  click on the balloons to pop  *
*  them before time ends.        *
*********************************/

@DISPLAY 1, {Width = 640, Height = 480, Color = #GRAY, Title = "Balloons"}

@BGPIC 2, "backgrounds/background.png"

;balloon sprites, each with 2 frames, one with the balloon image and one with the popped image
@SPRITE 1, "sprites/balloon_blue.png", {Width = 54, Height= 70, Frames = 2, FPR = 2, LoadAlpha = True}
@SPRITE 2, "sprites/balloon_black.png", {Width = 54, Height= 70, Frames = 2, FPR = 2, LoadAlpha = True}
@SPRITE 3, "sprites/balloon_green.png", {Width = 54, Height= 70, Frames = 2, FPR = 2, LoadAlpha = True}
@SPRITE 4, "sprites/balloon_red.png", {Width = 54, Height= 70, Frames = 2, FPR = 2, LoadAlpha = True}

;menu button brushes
@BRUSH 1, "brushes/play1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 2, "brushes/play2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 3, "brushes/player1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 4, "brushes/player2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 5, "brushes/scores1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 6, "brushes/scores2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 7, "brushes/exit1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 8, "brushes/exit2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 9, "brushes/close1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 10, "brushes/close2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 11, "brushes/ok1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 12, "brushes/ok2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 13, "brushes/medal1.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 14, "brushes/medal2.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 15, "brushes/medal3.png", {LoadAlpha = True, SmoothScale = True}
@BRUSH 16, "brushes/medal4.png", {LoadAlpha = True, SmoothScale = True}

;game samples
@SAMPLE 1, "sounds/menu.8svx"
@SAMPLE 2, "sounds/button.8svx"
@SAMPLE 3, "sounds/pop.8svx"
@SAMPLE 4, "sounds/quit.8svx"
@SAMPLE 5, "sounds/win.8svx"
@SAMPLE 6, "sounds/lose.8svx"

;include other source files
@INCLUDE "play.hws"				;game functions
@INCLUDE "balloon.hws"			;balloon object functions
@INCLUDE "language.hws"			;catalog function
@INCLUDE "scores.hws"			;highscores functions

EscapeQuit(True)

SetFillStyle(#FILLCOLOR)


player = {name = "", score = 0, time = 0, date = ""}	;player's data
balloons = 15											;total ballons on screen
time = 10												;total time in seconds
countdown = {}											;table for storing time numbers
scores = {}												;table for storing score numbers
highscores = {}											;highscores table
done = True												;is game over?
catalog$ = {}											;catalog table


/*******************************************
 * Mouse and keyboard events while playing *
 *******************************************/
Function p_EventFunction(msg)
    Switch(msg.action)
    Case "OnMouseDown":											;when mouse clicked
	    If done = False
	        p_Burst()											;check if balloon is hit
		EndIf
    Case "OnKeyDown":
	    If msg.key = " " And HaveObject(#TIMER, 1)				;check if SPACE pressed and timer 1 is running
	        PauseTimer(1)										;pause timer
	        WaitKeyDown("Space")								;pause the script until SPACE is pressed again
	        ResumeTimer(1)
	    EndIf
    EndSwitch
EndFunction


/*****************************************
*  Event table function for menu buttons *
******************************************/
Function p_Buttons(msg)
	Switch msg.action
	Case "OnMouseOver"
		If msg.id = button1
			HideLayer("play2")
			ShowLayer("play1")
		ElseIf msg.id = button2
			HideLayer("player2")
			ShowLayer("player1")
		ElseIf msg.id = button3
			HideLayer("scores2")
			ShowLayer("scores1")
		ElseIf msg.id = button4
			HideLayer("exit2")
			ShowLayer("exit1")
		ElseIf msg.id = button5
			HideLayer("close2")
			ShowLayer("close1")
		EndIf
	Case "OnMouseOut"
		If msg.id = button1
			HideLayer("play1")
			ShowLayer("play2")
		ElseIf msg.id = button2
			HideLayer("player1")
			ShowLayer("player2")
		ElseIf msg.id = button3
			HideLayer("scores1")
			ShowLayer("scores2")
		ElseIf msg.id = button4
			HideLayer("exit1")
			ShowLayer("exit2")
		ElseIf msg.id = button5
			HideLayer("close1")
			ShowLayer("close2")
		EndIf
	Case "OnMouseUp"
		If msg.id = button1					;play
			PlaySample(2)
			CloseDisplay(1)
			p_Play()
		ElseIf msg.id = button2				;set player's name
			PlaySample(2)
			p_SetName()
		ElseIf msg.id = button3				;show scores
			PlaySample(2)
			p_ShowScores()
		ElseIf msg.id = button4				;quit program
			PlaySample(4)
			Wait(25)
			End()
		ElseIf msg.id = button5				;close scores list
			SelectDisplay(1)
			FreeDisplay(3)
		ElseIf msg.id = button6				;"yes" to play again
			p_CleanUp()
	        p_Play()
		ElseIf msg.id = button7				;"no" to play again
			p_CleanUp()
			OpenDisplay(1)
			;p_Start()
		EndIf
	EndSwitch
EndFunction


/*****************
 * Game function *
 *****************/
Function p_Play()
	CreateDisplay(2, {BGPic = 2, Active = True, Title = "Balloons"})	;create and open game display
	OpenDisplay(2)
	DisableLayers()						;game display uses sprites so no layers
    done = False						;game is not over

	If (player.name = "")				;if no player name yet...
		p_SetName()
	EndIf
	
	player.time = 0						;reset time from last game played
	player.date = ""					;reset date from last game played
	
	InstallEventHandler({OnMouseDown = p_EventFunction, OnKeyDown = p_EventFunction})

    SetFont(#SANS, 18, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)
	SetFontColor(#WHITE)

    ;DisplayTransitionFX(2, {Type = #FADE, Speed = 50, Parameter = #WHITE})	;display background with a transition fx

    Box(0, 0, 640, 25, #GRAY, {Transparency = "50%"})          ;score and time area

    p_CreateScore()                                            ;set starting score
    p_CreateCountdown()                                        ;set remaining time
    Wait(25)
    p_CreateBalloons()                                         ;create and show balloons

    StartTimer(1)                                              ;start timer

    SetInterval(1, p_Countdown, 1000)                          ;execute timecountdown loop, 1 second interval
    SetInterval(2, p_MoveBalloons, 1000/25)                    ;execute main loop at 25fps
EndFunction


/*************
* Start menu *
**************/
Function p_Start()
	EnableLayers()
    SetFont(#SANS, 30, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)
	SetFillStyle(#FILLCOLOR)

	;events for start menu buttons
	evttable = {OnMouseUp = p_Buttons, OnMouseOver = p_Buttons, OnMouseOut = p_Buttons}

	DisplayTransitionFX(1, {Type = #FADE, Speed = 50, Parameter = #WHITE})	;display background with a transition fx

	PlaySample(1)		;play menu sample

	;create menu button 1
	button1Tag$ = CreateTextObject(Nil, catalog$[0], {Encoding=utf8})					;create button text from catalog
	bt1_tag = ConvertToBrush(#TEXTOBJECT, button1Tag$, Nil)								;convert text to brush
	FreeTextObject(button1Tag$)															;textobject no longer needed
	DisplayBrush(1, 235, 170, {Width = 35, Height = 37, Name = "play1", Hidden = True})
	DisplayBrush(2, 235, 170, {Width = 35, Height = 37, Name = "play2"})								;display button image using center as anchor
	DisplayBrush(bt1_tag, 280, 172)														;display text image using center as anchor
	bwidth = GetAttribute(#BRUSH, bt1_tag, #ATTRWIDTH)
	button1 = MakeButton(Nil, #SIMPLEBUTTON, 235, 172, 45 + bwidth, 35, evttable)			;create clickable area

	;create menu button 2
	button2Tag$ = CreateTextObject(Nil, catalog$[1], {Encoding=utf8})
	bt2_tag = ConvertToBrush(#TEXTOBJECT, button2Tag$, Nil)
	FreeTextObject(button2Tag$)
	DisplayBrush(3, 235, 235, {Width = 35, Height = 37, Name = "player1", Hidden = True})
	DisplayBrush(4, 235, 235, {Width = 35, Height = 37, Name = "player2"})
	DisplayBrush(bt2_tag, 280, 237)
	bwidth = GetAttribute(#BRUSH, bt2_tag, #ATTRWIDTH)
	button2 = MakeButton(Nil, #SIMPLEBUTTON, 235, 237, 45 + bwidth, 35, evttable)

	;create menu button 3
	button3Tag$ = CreateTextObject(Nil, catalog$[2], {Encoding=utf8})
	bt3_tag = ConvertToBrush(#TEXTOBJECT, button3Tag$, Nil)
	FreeTextObject(button3Tag$)
	DisplayBrush(5, 235, 300, {Width = 35, Height = 37, Name = "scores1", Hidden = True})
	DisplayBrush(6, 235, 300, {Width = 35, Height = 37, Name = "scores2"})
	DisplayBrush(bt3_tag, 280, 302)
	bwidth = GetAttribute(#BRUSH, bt3_tag, #ATTRWIDTH)
	button3 = MakeButton(Nil, #SIMPLEBUTTON, 235, 302, 45 + bwidth, 35, evttable)
	
	;create menu button 4
	button4Tag$ = CreateTextObject(Nil, catalog$[3], {Encoding=utf8})
	bt4_tag = ConvertToBrush(#TEXTOBJECT, button4Tag$, Nil)
	FreeTextObject(button4Tag$)
	DisplayBrush(7, 580, 430, {Width = 35, Height = 37, Name = "exit1", Hidden = True})
	DisplayBrush(8, 580, 430, {Width = 35, Height = 37, Name = "exit2"})
	bwidth = GetAttribute(#BRUSH, bt4_tag, #ATTRWIDTH)
	DisplayBrush(bt4_tag, 575 - bwidth, 432)
	button4 = MakeButton(Nil, #SIMPLEBUTTON, 575 - bwidth, 432, 40 + bwidth, 35, evttable)
EndFunction


p_ReadCatalog()			;read locale catalog
p_OpenScores()			;read highscores
p_Start()				;start game

Repeat
    WaitEvent()
Forever
