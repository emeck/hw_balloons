/*******************
 * Create balloons *
 *******************/
Function p_CreateBalloons()
    For Local i = 1 To balloons
        balloon[i] = balloon:new()                  ;create new balloon object
    Next
EndFunction


/*****************
 * Move balloons *
 *****************/
Function p_MoveBalloons()
    BeginRefresh()
    For Local i = 1 To balloons
        balloon[i]:move()                                               ;move balloon object
    Next
    EndRefresh()
EndFunction


/***************************
 * Check if balloon is hit *
 ***************************/
Function p_Burst()
    Local mx, my = MouseX(), MouseY()		                            ;store mouse position when clicked

    For Local i = balloons To 1 Step -1                                 ;negative step so click works on balloon "on top" in case of overlapping with other
        If balloon[i].notPopped
            Local bx, by = balloon[i].x, balloon[i].y                   ;copy balloon position

            ;check if balloon sprite was hit
	        If mx > bx And mx < bx + 54 And my > by And my < by + 70
	            balloon[i]:pop()                                        ;remove popped balloon
                RemoveSprite(scores[player.score])
                player.score = player.score + 1
	            DisplaySprite(scores[player.score], 70, 5)				;update score: shows next frame of scores sprite
	            If player.score = balloons
					player.time = GetTimer(1) / 1000 ;get elapsed time
		            p_GameOver()
	            EndIf
	            Break
            EndIf
        EndIf
    Next
EndFunction


/********************************************
 * Create scores and display starting score *
 ********************************************/
Function p_CreateScore()
    scoreText = CreateTextObject(Nil, "Score: ")                        ;create "Score" text

    For Local i = 0 To balloons
	    CreateTextObject(1, PadNum(i, 2))                               ;create numbers for score as text objects
	    scores[i] = CreateSprite(Nil, #TEXTOBJECT, 1)                   ;convert numbers to sprites
        FreeTextObject(1)		                                        ;free memory of no longer deeded text objects
    Next

    DisplayTextObject(scoreText, 10, 4)                                 ;display "Score" text
    DisplaySprite(scores[player.score], 70, 4)                                 ;display starting score
EndFunction


/**********************
 * Create time sprite *
 **********************/
Function p_CreateCountdown()
    timeText = CreateTextObject(Nil, "Time: ")                          ;create "Time" text

    For Local i = 0 To time
        CreateTextObject(1, PadNum(time - i, 2))                        ;create timer numbers as text objects starting at 10
        countdown[i] = CreateSprite(Nil, #TEXTOBJECT, 1)                ;convert numbers to sprites
        FreeTextObject(1)
    Next

    For Local i = 0 To time
        CreateTextObject(i, PadNum(time - i, 2))                        ;create timer numbers as text objects starting at 10
    Next

    DisplayTextObject(timeText, 550, 4)                                 ;display "Time" text
    DisplaySprite(countdown[0], 600, 4)                                 ;display starting time
EndFunction


/************************
 * Check time countdown *
 ************************/
Function p_Countdown()
    ;remember that time sprites run from 10 to 0
    Local t = Int(GetTimer(1) / 1000)       ;get next second

    ;when resuming after a pause, the display is redrawn and the time sprite is not in sync with the timer
    ;this checks for the sprite displayed when pausing and removes it before the next is displayed
    If t > 0
        If GetAttribute(#SPRITE, countdown[t-1], #ATTRONSCREEN) Then RemoveSprite(countdown[t-1])
    EndIf
    
    If t => time
	    DisplaySprite(countdown[t], 600, 4) ;display last time sprite (countdown[10]) which is 0
	    p_GameOver("lose")
    Else
        DisplaySprite(countdown[t], 600, 4) ;display next time sprite
    EndIf
EndFunction


/*************
 * Game over *
 *************/
Function p_GameOver(endText$)
    StopTimer(1)
    ClearInterval(2)
    ClearInterval(1)

    done = True             ;game is over

    RemoveSprites(True)     ;remove sprites but keep graphics displayed so box can be draw on top

    Box(0, 0, 640, 480, Color = #GRAY, {Transparency = "50%"})

    SetFont(#SANS, 36, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)

    If endText$ = "lose"
		PlaySample(6)
	    TextOut(#CENTER, #CENTER, "Game Over")
    Else
		PlaySample(5)
	    TextOut(#CENTER, #CENTER, "You won!")
		p_CompareTimes()
    EndIf

    Wait(100)
    TextOut(#CENTER, 260, "Play again? Y/N")
EndFunction


/************
 * Clean up *
 ************/
Function p_CleanUp()
    For Local i = 0 To balloons
	    FreeSprite(scores[i])             ;free sprites created for score
    Next

    For Local i = 0 To time
	    FreeSprite(countdown[i])           ;free sprites created for timer
    Next

    For Local i = 1 To balloons
	    FreeSprite(balloon[i].sprite)     ;free sprites created for balloons
    Next

    FreeTextObject(scoreText)             ;free text objects
    FreeTextObject(timeText)

    player.score = 0
EndFunction


/********************
* Set player's name *
*********************/
Function p_SetName()
	player.name = StringRequest("PlayerÂ´s name", "Write your name", "", #ALL, 18)
EndFunction


/******************
* Read highscores *
*******************/
Function p_OpenScores()
	If(Exists("highscores.bin"))						;check if file exists
		OpenFile(1, "highscores.bin", #MODE_READ)		;open file
		highscores = ReadTable(1)						;read table from file
		CloseFile(1)
	Else												;if file doesn't exist
		Local date$ = GetDate()							;get system date
		
		For Local i = 0 To 9							;insert 10 highscores
			InsertItem(highscores, {name = "Enrique", time = 9.999, date = date$})
		Next

		OpenFile(1, "highscores.bin", #MODE_WRITE)		;create and open file
		WriteTable(1, highscores)						;write table to file
		CloseFile(1)
	EndIf
EndFunction


/****************************************
* Compare player's time with highscores *
*****************************************/
Function p_CompareTimes()
	For Local i = 0 To 9
		If player.time < highscores[i].time
			InsertItem(highscores, {name = player.name, time = player.time, date = GetDate()}, i)	;insert item in position "i"
			RemoveItem(highscores)						;remove last item to keep 10 highscore in total
			p_WriteScores()
			Break
		EndIf
	Next
EndFunction


/************************
* Show highscores table *
*************************/
Function p_ShowScores()
	Local Col_A, Col_B, Col_C = 50, 250, 400
	Local Row = 15
	CreateDisplay(3, {Width = 610, Height = 430, Color = #GRAY, Active = True, Title = "Highscores"})

	SetFont(#SANS, 30, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)

	OpenDisplay(3)

	DisplayBrush(8, 480, 360, {Width = 100, Height = 50})
	button5Text = CreateTextObject(Nil, "Close", {Encoding=utf8})
	DisplayTextObject(button5Text, 495, 370)
	button5 = MakeButton(Nil, #SIMPLEBUTTON, 480, 360, 100, 50, evttable)

	SetFontColor(#BLACK)
	TextOut(Col_A, Row, "Name")
	TextOut(Col_B, Row, "Time")
	TextOut(Col_C, Row, "Date")
	Row = 20
	
	SetFontColor(#WHITE)
	SetFontStyle(#NORMAL)
	SetFontStyle(#ANTIALIAS)
	
	For Local i = 0 To 9
		Row = Row + 30
		;NPrint(highscores[i].name.." "..highscores[i].time.." "..highscores[i].date)
		TextOut(Col_A, Row, highscores[i].name)
		TextOut(Col_B, Row, highscores[i].time)
		TextOut(Col_C, Row, highscores[i].date)
	Next
EndFunction


/*************************
* Write highscores table *
**************************/
Function p_WriteScores()
		OpenFile(1, "highscores.bin", #MODE_WRITE)		;pen file to write
		WriteTable(1, highscores)						;write table to file
		CloseFile(1)
EndFunction