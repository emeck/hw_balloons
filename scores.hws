/********************
* Set player's name *
*********************/
Function p_SetName()
	player.name = StringRequest(catalog$[6], catalog$[7], "", #ALL, 10)
EndFunction


/******************
* Read highscores *
*******************/
Function p_OpenScores()
	If(Exists("highscores.bin"))							;check if file exists
		OpenFile(1, "highscores.bin", #MODE_READ)			;open file
		highscores = ReadTable(1)							;read table from file
		CloseFile(1)
	Else													;if file doesn't exist
		Local date$ = GetDate()								;get system date
		
		For Local i = 0 To 9								;insert 10 highscores
			InsertItem(highscores, {name = "Enrique", time = 9.999, date = date$})
		Next

		OpenFile(1, "highscores.bin", #MODE_WRITE)			;create and open file
		WriteTable(1, highscores)							;write table to file
		CloseFile(1)
	EndIf
EndFunction


/****************************************
* Compare player's time with highscores *
*****************************************/
Function p_CompareTimes()
	For Local i = 0 To 9
		If player.time < highscores[i].time
			InsertItem(highscores, {name = player.name,
									time = player.time,
									date = GetDate()}, i)	;insert item in position "i"
			RemoveItem(highscores)							;remove last item to keep 10 highscore in total
			p_WriteScores()
			Return("high")
		EndIf
	Next
	Return("low")
EndFunction


/************************
* Show highscores table *
*************************/
Function p_ShowScores()
	Local Col_A, Col_B, Col_C = 50, 250, 400
	Local Row = 15
	CreateDisplay(3, {Width = 610, Height = 430, Color = #GRAY, NoClose = True, Active = True, Title = catalog$[8]})

	SetFont(#SANS, 30, {Engine = #FONTENGINE_INBUILT})
    SetFontStyle(#BOLD)
    SetFontStyle(#ANTIALIAS)

	OpenDisplay(3)
	EnableLayers()
	
	;create and display close button
	button5Tag$ = CreateTextObject(Nil, catalog$[9], {Encoding=utf8})
	bt5_tag = ConvertToBrush(#TEXTOBJECT, button5Tag$, Nil)
	FreeTextObject(button5Tag$)
	DisplayBrush(9, 550, 380, {Name = "close1", Hidden = True})
	DisplayBrush(10, 550, 380, {Name = "close2"})
	bwidth = GetAttribute(#BRUSH, bt5_tag, #ATTRWIDTH)
	DisplayBrush(bt5_tag, 545 - bwidth, 382)
	button5 = MakeButton(Nil, #SIMPLEBUTTON, 545 - bwidth, 382, 40 + bwidth, 35, evttable)

	SetFontColor(#BLACK)
	TextOut(Col_A, Row, catalog$[1])
	TextOut(Col_B, Row, catalog$[4])
	TextOut(Col_C, Row, catalog$[5])
	Row = 20
	
	SetFontColor(#WHITE)
	SetFontStyle(#NORMAL)
	SetFontStyle(#ANTIALIAS)
	
	For Local i = 0 To 9
		Row = Row + 30
		TextOut(Col_A, Row, highscores[i].name)
		TextOut(Col_B, Row, highscores[i].time)
		TextOut(Col_C, Row, highscores[i].date)
	Next
EndFunction


/*************************
* Write highscores table *
**************************/
Function p_WriteScores()
		OpenFile(1, "highscores.bin", #MODE_WRITE)		;pen file to write
		WriteTable(1, highscores)						;write table to file
		CloseFile(1)
EndFunction