/********************
* Set player's name *
*********************/
Function p_SetName()
	player.name = StringRequest(catalog$[6], catalog$[7], "", #ALL, 10)
EndFunction


/******************
* Read highscores *
*******************/
Function p_OpenScores()
	If(Exists("highscores.bin"))							;check if file exists
		OpenFile(1, "highscores.bin", #MODE_READ)			;open file
		highscores = ReadTable(1)							;read table from file
		CloseFile(1)
	Else													;if file doesn't exist
		Local date$ = GetDate()								;get system date
		
		For Local i = 0 To 9								;insert 10 highscores
			InsertItem(highscores, {name = "---", time = 30.000, date = "---"})
		Next

		OpenFile(1, "highscores.bin", #MODE_WRITE)			;create and open file
		WriteTable(1, highscores)							;write table to file
		CloseFile(1)
	EndIf
EndFunction


/****************************************
* Compare player's time with highscores *
*****************************************/
Function p_CompareTimes()
	For Local i = 0 To 9
		If player.time < highscores[i].time
			;insert item in position "i"
			InsertItem(highscores, {name = player.name,	time = player.time,	date = GetDate()}, i)
			RemoveItem(highscores)				;remove last item to keep 10 highscore in total
			p_WriteScores()						;write new highscores table to file
			Return("high")
		EndIf
	Next
	Return("low")
EndFunction


/************************
* Show highscores table *
*************************/
Function p_ShowScores()
	menuOn = False								;to deactivate menu buttons effects that caused crash
	Local Col_A, Col_B, Col_C = 50, 250, 400	;set columns offset
	Local Row = 15								;set title line offset

	;create and open display for high scores
	CreateDisplay(3, {BGPic = 3, NoClose = True, Active = True, AlwaysOnTop = True, Title = catalog$[8]})
	SelectDisplay(3)	;to draw on display 3 while not openned
	EnableLayers()		;to be able to show/hide button's layers (button colored or grayed)
	
	;create and display close button
	p_setFontButtons()
	button5Tag$ = CreateTextObject(Nil, catalog$[9], {Encoding=utf8})
	bt5_tag = ConvertToBrush(#TEXTOBJECT, button5Tag$, Nil)
	FreeTextObject(button5Tag$)
	DisplayBrush(9, 550, 380, {Name = "close1", Hidden = True})
	DisplayBrush(10, 550, 380, {Name = "close2"})
	bwidth = GetAttribute(#BRUSH, bt5_tag, #ATTRWIDTH)			;take into account length of string
	DisplayBrush(bt5_tag, 545 - bwidth, 382)					;to display it accordingly
	button5 = MakeButton(Nil, #SIMPLEBUTTON, 545 - bwidth, 382, 40 + bwidth, 35, evttable)

	p_setFontHighScoresTitle()
	;print column titles
	TextOut(Col_A, Row, catalog$[1])
	TextOut(Col_B, Row, catalog$[4])
	TextOut(Col_C, Row, catalog$[5])

	Row = 25					;leave some space after column titles

	p_setFontHighScores()
	;print highscores
	For Local i = 0 To 9
		Row = Row + 30
		TextOut(Col_A, Row, highscores[i].name)
		TextOut(Col_B, Row, highscores[i].time)
		TextOut(Col_C, Row, highscores[i].date)
	Next
	OpenDisplay(3)
EndFunction


/*************************
* Close highscores table *
**************************/
Function p_CloseScores()
	RemoveLayers()				;removes the textout from display 3
	SelectDisplay(1)			;selects display 1 so we can later free display 3
	menuOn = True
	FreeDisplay(3)
EndFunction


/*************************
* Write highscores table *
**************************/
Function p_WriteScores()
		OpenFile(1, "highscores.bin", #MODE_WRITE)		;pen file to write
		WriteTable(1, highscores)						;write table to file
		CloseFile(1)
EndFunction